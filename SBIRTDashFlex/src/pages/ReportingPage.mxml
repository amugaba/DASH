<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009"
						xmlns:s="library://ns.adobe.com/flex/spark"
						xmlns:mx="library://ns.adobe.com/flex/mx"
						xmlns:questions="components.questions.*"
						xmlns:datareportservice="services.datareportservice.*"
						backgroundColor="#F2F9F1"  chromeColor="#82BE6D"
						contentBackgroundColor="#D8EFD3">
	<fx:Script>
		<![CDATA[
			import components.questions.*;
			import components.skips.*;
			import components.validators.Valid;
			
			import flashx.textLayout.conversion.TextConverter;
			import flashx.textLayout.edit.IEditManager;
			import flashx.textLayout.elements.FlowElement;
			import flashx.textLayout.elements.FlowGroupElement;
			import flashx.textLayout.elements.LinkElement;
			import flashx.textLayout.events.FlowElementMouseEvent;
			
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.HRule;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.events.ValidationResultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.validators.ValidationResult;
			
			import spark.components.Button;
			import spark.components.FormItem;
			import spark.components.NavigatorContent;
			import spark.components.RichEditableText;
			import spark.components.RichText;
			import spark.primitives.Line;
			
			import valueObjects.DataReportVO;
			
			protected var global:SBIRTDashFlex = FlexGlobals.topLevelApplication as SBIRTDashFlex;
			private var created:Boolean = false;
			private var QUESTION_INDENT:int = 20;
			
			public var updatingData:Boolean = false;
			private var year1:int = new Date().getFullYear();
			private var year2:int = year1 - 1;
			
			private var month:QuestionList = new QuestionList("month","For what month are you reporting data?",global.monthList,-1);
			private var year:QuestionList = new QuestionList("year","For what year?", new ArrayList([year1, year2]),0);
			private var uniquePatients:QuestionTextRefusable = new QuestionTextRefusable("uniquePatients","During <b><u>____</b></u>, how many <a>unique patients</a> aged 18 or older had a primary care visit?","");
			
			private var uniquePatientsLabel:QuestionClass = new QuestionClass("","<b>Of those <u>____</u> unique patients...</b><br>");
			private var patientsEligible:QuestionTextRefusable = new QuestionTextRefusable("patientsEligible","How many patients <a>had not yet been prescreened in the last year?</a>","");
			private var prescreensEligible:QuestionTextRefusable = new QuestionTextRefusable("prescreens","How many did you prescreen that had NOT received a prescreen in the last year?","");
			private var prescreensIneligible:QuestionTextRefusable = new QuestionTextRefusable("validPrescreens","How many did you prescreen that had already received a prescreen in the last year?","");
			
			private var prescreensLabel:QuestionClass = new QuestionClass("","<b>Of those <u>____</u> total patients prescreened...</b><br>");
			private var alcoholPrescreens:QuestionTextRefusable = new QuestionTextRefusable("alcoholPrescreens","How many were positive for alcohol but negative for drugs?","");
			private var drugPrescreens:QuestionTextRefusable = new QuestionTextRefusable("drugPrescreens","How many were positive for drugs but negative for alcohol?","");
			private var bothPrescreens:QuestionTextRefusable = new QuestionTextRefusable("bothPrescreens","How many were positive for both alcohol and drugs?","");
			
			private var screensAll:QuestionTextRefusable = new QuestionTextRefusable("screens","Of those <b><u>____</b></u> positive patients, how many received a full screening?","");
			private var isNotScreening:QuestionList = new QuestionList("isNotScreening","Does your site prescreen some patients in buildings, units, or other areas where it is not yet possible " +
				"for those patients to receive a full screening?", new ArrayList(["No","Yes"]),0);
			
			//If not screening some patients
			private var possibleScreens:QuestionTextRefusable = new QuestionTextRefusable("possibleScreens","Of the <b><u>____</b></u> total positive prescreens, how many occurred in an area of your clinic where it is <a>possible to receive a full screening</a>?","");
			private var screensOfPossible:QuestionTextRefusable = new QuestionTextRefusable("screensOfPossible","Of those <b><u>____</b></u> positive patients that were in a clinic area that does full screenings, how many received a full screening?","");			
			
			private var screeningsLabel:QuestionClass = new QuestionClass("","<b><br>Of those <u>____</u> full screenings...</b><br>");
			private var educationScore:QuestionTextRefusable = new QuestionTextRefusable("educationScores","How many scored ‘Education’?","");
			private var biScore:QuestionTextRefusable = new QuestionTextRefusable("biScores","How many scored ‘Brief Intervention’?","");
			private var btScore:QuestionTextRefusable = new QuestionTextRefusable("btScores","How many scored ‘Brief Treatment’?","");
			private var rtScore:QuestionTextRefusable = new QuestionTextRefusable("rtScores","How many scored ‘Referral to Treatment’?","");
			
			private var servicesLabel:QuestionClass = new QuestionClass("","<b><a>Services</a></b>");
			private var biServices:QuestionTextRefusable = new QuestionTextRefusable("bis","How many Brief Interventions were performed?","");
			private var btServices:QuestionTextRefusable = new QuestionTextRefusable("bts","How many Brief Treatments were performed?","");
			private var rtServices:QuestionTextRefusable = new QuestionTextRefusable("rts","How many Referrals to Treatment were performed?","");
			
			private var otherComments:QuestionTextArea = new QuestionTextArea("<b>Other comments:</b>","");
			
			public function initPage():void
			{
				if(created)
				{
					resetPage();
					return;
				}
				else
					created = true;
				
				reportingForm.createText("All fields are required. If you are unable to answer a question, use the drop-down and select \"Missing Data\"","h1");
				
				//Add questions
				addQuestionOther(month, reportingForm);
				addQuestionOther(year, reportingForm);
				addQuestionNumber(uniquePatients, reportingForm);
				uniquePatients.addHelpText("If a patient came for multiple visits, only count that patient once.");
				reportingForm.addDivider();
				
				reportingForm.addQuestion(uniquePatientsLabel);
				addQuestionNumber(patientsEligible, reportingForm);
				patientsEligible.addHelpText("This is the number of patients who were due for their annual SBIRT screening.");
				addQuestionNumber(prescreensEligible, reportingForm);
				addQuestionNumber(prescreensIneligible, reportingForm);
				reportingForm.addDivider();
				
				reportingForm.addElement(prescreensLabel);
				addQuestionNumber(alcoholPrescreens, reportingForm);
				addQuestionNumber(drugPrescreens, reportingForm);
				addQuestionNumber(bothPrescreens, reportingForm);
				reportingForm.addDivider();
				
				reportingForm.createText("Screenings","h1");
				addQuestionNumber(screensAll, reportingForm);
				addQuestionOther(isNotScreening, reportingForm);
				addQuestionNumber(possibleScreens, reportingForm);
				addQuestionNumber(screensOfPossible, reportingForm);
				
				reportingForm2.addElement(screeningsLabel);
				addQuestionNumber(educationScore, reportingForm2);
				addQuestionNumber(biScore, reportingForm2);
				addQuestionNumber(btScore, reportingForm2);
				addQuestionNumber(rtScore, reportingForm2);
				reportingForm2.addDivider();
				
				reportingForm2.addElement(servicesLabel);
				servicesLabel.addHelpText("The ‘services performed’ numbers may differ from the above ‘screening scores’ numbers. \n\nThis is because the behavioral health specialist may provide a higher level intervention than the screening score indicates based on clinical judgment. There also may be situations where a service cannot be performed due to time or other constraints.");
				addQuestionNumber(biServices, reportingForm2);
				addQuestionNumber(btServices, reportingForm2);
				addQuestionNumber(rtServices, reportingForm2);
				reportingForm2.addDivider();
				
				reportingForm2.addQuestion(otherComments);
				
				//Bind question answers
				month.bindToQuestionLabel(uniquePatients,"____");
				uniquePatients.bindToQuestionLabel(uniquePatientsLabel,"____");
				//uniquePatients.bindToQuestionLabel(prescreensAll,"____");
				//prescreensAll.bindToQuestionLabel(prescreensLabel,"____");
				screensAll.bindToQuestionLabel(screeningsLabel,"____");
				screensAll.bindToQuestionLabel(possibleScreens,"____");
				possibleScreens.bindToQuestionLabel(screensOfPossible,"____");
				
				reportingForm.addSkipDropDown(isNotScreening,["No"],[possibleScreens,screensOfPossible]);

				prescreensEligible.bindFunction(replacePrescreensLabel);
				prescreensIneligible.bindFunction(replacePrescreensLabel);
				
				alcoholPrescreens.bindFunction(replaceScreeningsLabel);
				drugPrescreens.bindFunction(replaceScreeningsLabel);
				bothPrescreens.bindFunction(replaceScreeningsLabel);
				
				//Add Validators
				Valid.addNumberValidator(month,0);
			}
			
			protected function addQuestionNumber(q:QuestionClass, form:QuestionForm):void
			{
				form.addQuestion(q);
				q.text.setStyle("paddingLeft",QUESTION_INDENT);
				Valid.addNumberValidator(q,0,Number.NaN,true);
			}
			
			protected function addQuestionOther(q:QuestionClass, form:QuestionForm):void
			{
				form.addQuestion(q);
				q.text.setStyle("paddingLeft",QUESTION_INDENT);
				Valid.addRequiredValidator(q);
			}
			
			public function replacePrescreensLabel(val:String):void
			{
				var sum:int = 0;
				
				if(!isNaN(parseInt(prescreensEligible.answer)))
					sum += parseInt(prescreensEligible.answer);
				
				if(!isNaN(parseInt(prescreensIneligible.answer)))
					sum += parseInt(prescreensIneligible.answer);
				
				if(sum != 0)
				{
					var newLabel:String = prescreensLabel.baseLabel.replace("____",sum);
					prescreensLabel.label = newLabel;
				}
				else
					prescreensLabel.label = prescreensLabel.baseLabel;
			}
			
			public function replaceScreeningsLabel(val:String):void
			{
				var sum:int = 0;

				if(!isNaN(parseInt(alcoholPrescreens.answer)))
					sum += parseInt(alcoholPrescreens.answer);

				if(!isNaN(parseInt(drugPrescreens.answer)))
					sum += parseInt(drugPrescreens.answer);

				if(!isNaN(parseInt(bothPrescreens.answer)))
					sum += parseInt(bothPrescreens.answer);
				
				if(sum != 0)
				{
					var newLabel:String = screensAll.baseLabel.replace("____",sum);
					screensAll.label = newLabel;
				}
				else
					screensAll.label = screensAll.baseLabel;
			}
			
			protected function resetPage():void
			{
				tabNavigator.selectedIndex = 0; 
				updatingData = false;
				month.input.enabled = true;
				year.input.enabled = true;
				submitButton.label = "Submit Data!";
				reportingForm.reset();
				reportingForm2.reset();
			}
			
			protected function submitButton_clickHandler(event:MouseEvent):void
			{
				var results:ArrayList = reportingForm.validateForm();
				var results2:ArrayList = reportingForm2.validateForm();
				if(results.length > 0 || results2.length > 0)
				{
					Alert.show("Please complete all fields with valid answers.","Validation Errors");
					return;
				}
	
				//if not updating form, the report date has to be unique and in the past
				if(!updatingData)
				{
					var reportDate:Date = new Date(parseInt(year.answer), month.getAnswerIndex()-1, 1);
					if(reportDate > new Date())
						Alert.show("The report's date cannot be in the future.", "Validation Errors");
					else					
						reportExistsResult.token = dataReportService.reportExists(global.activeUser.autoid, month.getAnswerIndex()+1, parseInt(year.answer));
				}
				else
					parseForm();
			}
			
			protected function reportExistsResult_resultHandler(event:ResultEvent):void
			{
				if(reportExistsResult.lastResult)
					Alert.show("A report with this month and year has already been submitted.", "Validation Errors");
				else
					parseForm();
			}
			
			protected function parseForm():void
			{
				var report:DataReportVO = new DataReportVO();
				report.userid = global.activeUser.autoid;
				report.month = month.getAnswerIndex() + 1;
				report.year = parseInt(year.answer);
				
				if(updatingData)
					report = global.activeReport;
				
				report.uniquePatients = parseInt(uniquePatients.answer);
				report.patientsEligible = parseInt(patientsEligible.answer);
				report.validPrescreens = parseInt(prescreensEligible.answer);
				report.prescreens = report.validPrescreens + parseInt(prescreensIneligible.answer);
				report.alcoholPrescreens = parseInt(alcoholPrescreens.answer);
				report.drugPrescreens = parseInt(drugPrescreens.answer);
				report.bothPrescreens = parseInt(bothPrescreens.answer);
				report.screens = parseInt(screensAll.answer);
				report.isNotScreening = isNotScreening.getAnswerIndex();
				report.possibleScreens = parseInt(possibleScreens.answer);
				report.screensOfPossible = parseInt(screensOfPossible.answer);
				report.educationScores = parseInt(educationScore.answer);
				report.biScores = parseInt(biScore.answer);
				report.btScores = parseInt(btScore.answer);
				report.rtScores = parseInt(rtScore.answer);
				report.bis = parseInt(biServices.answer);
				report.bts = parseInt(btServices.answer);
				report.rts = parseInt(rtServices.answer);
				report.otherComments = otherComments.answer;
				
				if(!updatingData)
					createDataReportResult.token = dataReportService.createDataReport(report);
				else
					updateDataReportResult.token = dataReportService.updateDataReport(report);
			}
			
			public function populate(report:DataReportVO):void
			{
				updatingData = true;
				submitButton.label = "Update Data!";
				month.input.enabled = false;
				year.input.enabled = false;
				month.setAnswerIndex(report.month-1);
				year.setAnswerIndex(year1 - report.year + 1);
				uniquePatients.answer = report.uniquePatients.toString(); 
				patientsEligible.answer = report.patientsEligible.toString();
				prescreensEligible.answer = report.validPrescreens.toString();
				prescreensIneligible.answer = (report.prescreens - report.validPrescreens).toString();
				alcoholPrescreens.answer = report.alcoholPrescreens.toString();
				drugPrescreens.answer = report.drugPrescreens.toString();
				bothPrescreens.answer = report.bothPrescreens.toString();
				screensAll.answer = report.screens.toString();
				isNotScreening.setAnswerIndex(report.isNotScreening);
				possibleScreens.answer = report.possibleScreens.toString();
				screensOfPossible.answer = report.screensOfPossible.toString();
				educationScore.answer = report.educationScores.toString();
				biScore.answer = report.biScores.toString();
				btScore.answer = report.btScores.toString();
				rtScore.answer = report.rtScores.toString();
				biServices.answer = report.bis.toString();
				btServices.answer = report.bts.toString();
				rtServices.answer = report.rts.toString();
				otherComments.answer = report.otherComments;
			}
			
			protected function createDataReportResult_resultHandler(event:ResultEvent):void
			{
				var report:DataReportVO = createDataReportResult.lastResult;
				if(report != null)
				{
					Alert.show("Report submitted successfully!");
					global.activeReport = report;
					updatingData = true;
					submitButton.label = "Update Data!";
				}
			}
			
			protected function updateDataReportResult_resultHandler(event:ResultEvent):void
			{
				var report:DataReportVO = updateDataReportResult.lastResult;
				if(report != null)
					Alert.show("Report updated successfully!");
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:CallResponder id="createDataReportResult" result="createDataReportResult_resultHandler(event)"/>
		<datareportservice:DataReportService id="dataReportService"
											 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
											 showBusyCursor="true"/>
		<s:CallResponder id="reportExistsResult" result="reportExistsResult_resultHandler(event)"/>
		<s:CallResponder id="updateDataReportResult" result="updateDataReportResult_resultHandler(event)"/>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<mx:TabNavigator id="tabNavigator" x="7" y="24" width="685" height="650" paddingTop="0">
		<s:NavigatorContent width="100%" height="100%" label="Page 1">
			<questions:QuestionForm id="reportingForm" width="100%" height="100%" labelWidth="450">
			</questions:QuestionForm>
		</s:NavigatorContent>
		<s:NavigatorContent width="100%" height="100%" label="Page 2">
			<questions:QuestionForm id="reportingForm2" width="100%" height="100%" labelWidth="450">
			</questions:QuestionForm>
			<s:Button id="submitButton" x="312" y="495" height="42" label="Save"
					  click="submitButton_clickHandler(event)"/>
		</s:NavigatorContent>
	</mx:TabNavigator>
	<s:Label x="246" y="16" fontSize="22" fontWeight="normal" text="SBIRT Data Reporting"/>
	
</s:NavigatorContent>
